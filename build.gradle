import org.ajoberstar.grgit.Grgit

group 'tech.macil.minecraft.noneuclid'

apply plugin: 'java'
apply plugin: 'net.researchgate.release'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }

    //maven { url "http://repo.dmulloy2.net/nexus/repository/public/" }
    //ProtocolLib's maven entry is busted. Do this hack instead to just get the jar:
    ivy {
        url "http://repo.dmulloy2.net/nexus/repository/public/com/comphenix/protocol/ProtocolLib-API/4.3.0/"
        layout 'pattern' , {
            ivy '[module]-ivy.xml'
            artifact '[module]-[revision](.[ext])'
        }
    }
}

dependencies {
    compile 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    compile group: 'com.comphenix.protocol', name: 'ProtocolLib-API', version: '4.3.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.mockito:mockito-core:2.+"
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.6.0'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

if (!project.hasProperty("gitCommitHash")) {
    try {
        def repo = Grgit.open(dir: '.')
        ext.gitCommitHash = repo.head().abbreviatedId
    } catch (Exception e) {
        println "Error getting commit hash: " + e.getMessage()
    }
}
if (!project.hasProperty("gitCommitHash")) {
    ext.gitCommitHash = "no_git_id"
}

ext.internalVersion = version + ";" + gitCommitHash
jar.archiveName = rootProject.name + '.jar'

processResources {
    inputs.property("version", project.internalVersion)

    from (sourceSets.main.resources.srcDirs) {
        expand 'internalVersion': project.internalVersion
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

// Bukkit dependency is always a snapshot version
checkSnapshotDependencies.enabled = false
